plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.10.0"
}

group "argelbargel.gradle.plugins"
archivesBaseName = 'build-modules'
if (project.hasProperty('release') && project.getProperty('release') != '') {
    version = project.getProperty('release')
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

publishing {
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

dependencies {
    compile "org.apache.commons:commons-lang3:3.7"
    compile "commons-io:commons-io:2.6"
    compile 'org.ajoberstar:grgit:2.1.0'
}

dependencies {
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all' // use the version that is distributed with Gradle
    }
}

gradlePlugin {
    plugins {
        buildModule {
            id = 'argelbargel.build-modules.module'
            implementationClass = "${group}.buildModules.module.BuildModulePlugin"
        }
        buildModuleManager {
            id = 'argelbargel.build-modules.manager'
            implementationClass = "${group}.buildModules.manager.BuildModuleManagerPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/Argelbargel/gradle-build-modules'
    vcsUrl = 'https://github.com/Argelbargel/gradle-build-modules'
    description = 'Gradle-Plugin which allows to create a gradle-build which ties together multiple otherwise separate gradle-projects. It\'s use-case is quite similar to that of gradle\'s own Composite builds.'
    tags = ['build', 'modules']

    plugins {
        buildModule {
            displayName = 'Build-Module-Plugin'
        }

        buildModuleManager {
            displayName = 'Build-Module-Manager-Plugin'
        }
    }
}
